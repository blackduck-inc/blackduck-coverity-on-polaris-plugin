buildscript {
    apply from: "https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-repositories.gradle", to: buildscript
    apply from: 'https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-dependencies.gradle', to: buildscript
}

plugins {
    id 'org.jenkins-ci.jpi' version '0.38.0'
}

group = 'org.jenkins-ci.plugins'
version = '1.3.2-SNAPSHOT'
description = 'Allows users to run Polaris Software Integrity Platform static analysis in their builds'

apply plugin: 'com.synopsys.integration.solution'
apply from: 'docs.gradle'

artifactory {
    publish {
        defaults { publications('mavenJpi') }
    }
}

repositories {
    //jcenter is going away,exclude any dependency search https://blog.gradle.org/jcenter-shutdown
    maven { url "https://repo1.maven.org/maven2/" }
    maven { url "https://sig-artifactory.internal.synopsys.com/artifactory/ngp-snapshot-local" }
    maven { url "https://sig-artifactory.internal.synopsys.com/artifactory/plugins-releases" }
    maven { url "https://artifacts-oss.talend.com/nexus/content/repositories/TalendOpenSourceRelease/" }
    maven { url "https://repository.mulesoft.org/nexus/content/repositories/public/" }
}


jenkinsPlugin {
    // Must be compatible with the version declared in Jenkins Common
    coreVersion = '2.150.3'
    displayName = 'Synopsys Polaris Software Integrity Platform Plugin'
    url = 'https://github.com/jenkinsci/synopsys-polaris-plugin'
    gitHubUrl = 'https://github.com/jenkinsci/synopsys-polaris-plugin'

    compatibleSinceVersion = '1.0.0'
    pluginFirstClassLoader = false

    // "maskClasses" masks classes from the above specified coreVersion of Jenkins, not whatever the runtime Jenkins is. If the above version uses commons-compress 1.10 and runtime uses 1.18,
    // masking commons-compress here will have no effect.
    // -rotte MAR 2020
    // The methods we use to unpack the Polaris CLI utilize a version of commons-compress that is incompatible with version 2.197 of Jenkins and earlier.
    // Once we no longer depend on commons compress, no longer use those methods, or we update our minimum supported version past 2.197, we should unmask those classes.
    // -rotte FEB 2020
    maskClasses = 'org.apache.commons.compress'
}

dependencies {
    annotationProcessor 'com.synopsys.integration:jenkins-annotation-processor:0.0.1'

    implementation 'com.synopsys.integration:jenkins-common:0.3.4'

    implementation group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.18'

    // Must be compatible with the versions declared in Jenkins Common
    jenkinsPlugins 'org.jenkins-ci.plugins:credentials:2.1.10'
    jenkinsPlugins 'org.jenkins-ci.plugins:plain-credentials:1.0'

    implementation (group: 'com.atlassian.jira', name: 'jira-rest-java-client-app', version: '5.2.0') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    optionalJenkinsPlugins 'org.jenkins-ci.plugins:job-dsl:1.67'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-job:2.9'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps:2.23'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.10'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'

    testCompile group: 'org.jenkins-ci.main', name: 'jenkins-test-harness', version: '2.58'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
}
configurations.all {
    resolutionStrategy {
        force 'xalan:xalan:2.7.2'
        force 'log4j:log4j:1.2.17-cloudera1'
        force 'net.minidev:json-smart:2.4.7'
        force 'org.springframework:spring-jdbc:5.3.13'
        force 'org.codehaus.groovy:groovy-all:2.5.15'
        force 'org.acegisecurity:acegi-security:1.0.7-osgi'
        force 'org.eclipse.jetty:jetty-server:9.4.44.v20210927'
        force 'org.eclipse.jetty:jetty-servlet:9.4.44.v20210927'
        force 'org.eclipse.jetty:jetty-webapp:9.4.44.v20210927'
        force 'org.eclipse.jetty:jetty-xml:9.4.44.v20210927'
        force 'org.eclipse.jetty:jetty-security:9.4.44.v20210927'
        force 'org.eclipse.jetty:jetty-http:9.4.44.v20210927'
        force 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
        force 'commons-beanutils:commons-beanutils:1.9.4-talend'
        force 'org.apache.commons:commons-text:1.10.0'
        force 'org.codehaus.jettison:jettison:1.5.2'
    }
}
